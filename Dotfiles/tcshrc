if ($tty == '') then
    exit 0
endif
#{{{ Env
setenv  EDITOR      vim
setenv  VISUAL      vim
setenv  PAGER       more
setenv  LESS        "-erX"
setenv  GIT_PAGER   less
setenv  LANG        en_US.UTF-8
setenv  LC_TYPE     en_US.UTF-8

if ${?TERM} then
    switch( $TERM )
    case "xterm*":
        setenv TITLE "%{\033]0;%n@%m [%~]\007%}"
        breaksw;
    default:
        setenv TITLE ""
        breaksw;
    endsw
endif
#}}}
#{{{ Env tcsh
set myuid       = `id -u`
set stty        = `echo $tty | sed 's/tty//'`
set prompt      = '[%B%n@%m%b] %B%~%b%# '
set prompt2     = "%n@%M-$tty >%R?<%# "
set time        = ( 1 "%Uu %Ss %Er %Pcpu %X+%Dk %I+%Oio %Fpf+%Wsw" )
set who         = "%n has %a %l from %M"
set fignore     = ( .o \~ \#\*\# )
set complete    = enhance
set watch       = ( 1 any any )
set autolist    = ambiguous
set correct     = cmd
set listjobs    = long
set history     = ( 5000 "%h (%D-%W-%Y %T) %R\n" )
set savehist    = ( 5000 merge )
set histfile    = ~/.tcsh_history
set histdup     = erase
set matchbeep   = nomatch
set promptchars = '>#'
set listflags   = 'a'
set printexitvalue
set autoexpand
set listlinks
set echo_style  = both
unset autologout

bindkey "^R"    history-search-backward
bindkey "^S"    history-search-forward
#}}}
#{{{ Alias
alias   ll          'ls -lha \!* | more'
alias   l           'ls -lh \!* | more'
alias   vi          'vim \!*'
unalias ls
alias   rm          '\rm -i'
alias   src         'source ~/.tcshrc'
# Usage : cd /src/dir ; taroverssh root@dsthost /dst/dir
alias   taroverssh  'tar cvpf - . | ssh \!:1 tar xpf - -C \!:2'
alias   screenshot  'import -window root `hostname`-`date +%y%m%w-%H%M%S`.png'
alias   monip       'wget -q http://panel.depad.fr/ip.php -O - ; echo ""'
#}}}
#{{{ Completion
if ( -o ~/complete.tcsh ) then
    source ~/complete.tcsh
endif

if ( -o ~/complete.tcsh ) then
    source ~/complete.tcsh
endif
#}}}
#{{{ OS
switch($OSTYPE)
case darwin:
    setenv PATH /opt/local/bin:/opt/local/sbin:$PATH
    if ( -d /usr/local/mysql/bin ) then
        setenv PATH /usr/local/mysql/bin:$PATH
    endif
    alias  ggcontactsync '/System/Library/PrivateFrameworks/GoogleContactSync.framework/Versions/A/Resources/gconsync --sync com.google.ContactSync'
    alias  getmysqlbox 'mysqldump -A --opt --routines -hbox.depad.fr | mysql'
    breaksw
case FreeBSD:
    alias htop 'systat -vmstat 1'
    breaksw
default:
    breaksw
endsw
#{{{ Debian
if ( -x /usr/bin/apt-get ) then
    if ($myuid == 0) then
        alias updateall 'apt-get update && apt-get upgrade && apt-get autoclean && apt-get dist-upgrade'
    endif
endif
#}}}
#}}}
setenv PATH /sbin:/usr/sbin:$PATH
